import org.jetbrains.compose.desktop.application.dsl.TargetFormat
import org.jetbrains.kotlin.gradle.ExperimentalKotlinGradlePluginApi
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import java.util.*

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.kotlinSerialization)
    alias(libs.plugins.ksp)
    alias(libs.plugins.ktorfit)
}

kotlin {
    jvm("desktop") {
        compilations["test"].defaultSourceSet {
            dependencies {
//                implementation(kotlin("test"))
                implementation(libs.kotlin.test)
                implementation(libs.ktor.mock)
            }
        }
    }

    sourceSets {
        val desktopMain by getting

        androidMain.dependencies {
            implementation(compose.preview)
            implementation(libs.androidx.activity.compose)
            implementation(libs.decompose)
        }
        commonMain.dependencies {
            implementation(compose.runtime)
            implementation(compose.foundation)
            implementation(compose.material3)
            implementation(compose.materialIconsExtended)
            implementation(compose.ui)
            implementation(compose.components.resources)
            implementation(compose.components.uiToolingPreview)
            implementation(libs.androidx.lifecycle.viewmodel)
            implementation(libs.androidx.lifecycle.runtime.compose)
            implementation(libs.ktor.client.core)
            implementation(libs.ktor.client.auth)
            implementation(libs.ktor.client.content.negotiation)
            implementation(libs.ktor.serialization.kotlinx.json)
            implementation(libs.ktor.client.cio)
            implementation(libs.kotlinx.datetime)
            implementation(libs.decompose)
            implementation(libs.decompose.compose)
            implementation(libs.serialization.json)
            implementation(libs.koalaplot.core)
            implementation(libs.ktorfit.lib)
            implementation(project.dependencies.platform(libs.koin.bom))
            implementation(libs.koin.core)
        }
        desktopMain.dependencies {
            implementation(compose.desktop.currentOs)
            implementation(libs.kotlinx.coroutines.swing)
        }
        commonTest.dependencies {
                implementation(libs.kotlin.test) // Testy Kotlin (z TOML)
                implementation(libs.ktor.mock)  // Mock Engine dla Ktor
        }
    }
    sourceSets.commonMain.dependencies {
        implementation(kotlin("reflect"))
    }

    androidTarget {
        @OptIn(ExperimentalKotlinGradlePluginApi::class)
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_21)
        }
    }
}



val localPropertiesFile = rootProject.file("local.properties")
val localProperties = Properties()

if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.inputStream())
}

android {
    namespace = "pw.edu.pl.pap"
    compileSdk = libs.versions.android.compileSdk.get().toInt()

    defaultConfig {
        applicationId = "pw.edu.pl.pap"
        minSdk = libs.versions.android.minSdk.get().toInt()
        targetSdk = libs.versions.android.targetSdk.get().toInt()
        versionCode = 1
        versionName = "1.0"
        buildConfigField(
            "String",
            "BASE_URL",
            "\"${localProperties.getProperty("baseUrl", "localhost:8090")}\""
        )
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
    buildTypes {
        getByName("release") {
            isMinifyEnabled = false
        }
    }
    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    testOptions {
        unitTests.isReturnDefaultValues = true
    }
}

dependencies {
    debugImplementation(compose.uiTooling)
}

compose.desktop {
    application {
        mainClass = "pw.edu.pl.pap.MainKt"

        nativeDistributions {
            targetFormats(TargetFormat.AppImage, TargetFormat.Exe, TargetFormat.Deb, TargetFormat.Rpm)
            packageName = "pw.edu.pl.pap"
            packageVersion = "1.0.0"
        }
    }
}

tasks.withType<ProcessResources> {
    filesMatching("res/xml/network_security_config.xml") {
        expand("ip" to localProperties.getProperty("baseUrl", "localhost:8080"))
    }
}

tasks.withType<Test> {
    useJUnitPlatform()  // Wykorzystanie platformy JUnit do testów
    jvmArgs = listOf("-Dorg.gradle.parallel=true")  // Dodatkowe parametry JVM, jeśli są wymagane
    testLogging {
        events("passed", "skipped", "failed")  // Wyświetlanie wyników testów
    }
}

//tasks.withType<Test> {
//    useJUnitPlatform()
//    testLogging {
//        events("passed", "skipped", "failed")
//    }
//}